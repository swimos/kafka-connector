plugins {
  id 'java-library'
}

group = 'org.swimos'
description = 'Swim Kafka Connector'
version = property('application.version')
ext.swimVersion = project.property('swim.version')
sourceCompatibility = 11
targetCompatibility = 11

dependencies {
  api group: 'org.swimos', name: 'swim-api', version: swimVersion
  api group: 'org.swimos', name: 'swim-server', version: swimVersion
  api group: 'org.swimos', name: 'swim-client', version: swimVersion
  implementation group: 'org.apache.kafka', name: 'kafka-clients', version: '2.5.0'
  implementation group: 'org.apache.kafka', name: 'connect-api', version: '3.2.0'
  testImplementation 'org.testng:testng:7.4.0'
}

repositories {
  mavenCentral()
}

compileJava {
  options.compilerArgs += ['-Xlint:all']
  options.encoding = 'UTF-8'
}

test {
  useTestNG()
  scanForTestClasses = false
  include '**/*Spec.class'
  maxHeapSize = '1024m'

  testLogging {
    events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
    exceptionFormat 'full'
    displayGranularity = 4
  }

  outputs.upToDateWhen { false }
}

task copyPackage(type: Copy) {
  from "package"
  into "${buildDir}/swim-kafka-connect/swim-kafka-connect"
}

task copyLicense(type: Copy) {
  from "."
  include "LICENSE"
  into "${buildDir}/swim-kafka-connect/swim-kafka-connect/doc"
}

task copyLibs(type: Copy) {
  from project.tasks.jar.outputs
  from configurations.runtimeClasspath
  include "swim*.jar"
  into "${buildDir}/swim-kafka-connect/swim-kafka-connect/lib"
}

task createKafkaConnectPackage(type: Zip) {
  mkdir "${buildDir}/swim-kafka-connect/swim-kafka-connect"
  dependsOn copyPackage
  dependsOn copyLicense
  dependsOn copyLibs
  archiveFileName = "swim-kafka-connect.zip"
  destinationDirectory = layout.buildDirectory.dir("${buildDir}")
  from layout.buildDirectory.dir("${buildDir}/swim-kafka-connect")
}
